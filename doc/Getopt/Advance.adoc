= NAME

:vbar: |

Getopt::Advance - An advance command line option parsing tools.

= SYNOPSIS

[source,perl6]
-------------------------------
#!/usr/bin/env perl6

use Getopt::Advance;
use Getopt::Advance::Parser;
use Getopt::Advance::Exception;

my @files = [];
my OptionSet $optset .= new;

$optset.insert-pos(
    "directory",
    0,
    sub ($, $dirarg) {
        die "$dirarg: Not a valid directory" if $dirarg.value.IO !~~ :d;
        @files = gather &find($dirarg.value.IO);
    }
);
$optset.append(
    "h|help=b"      => "print this help.",
    "v|version=b"   => "print program version.",
    "?=b"           => "same as -h.",
    :multi
);
$optset.append(
    'd=b' => 'specify file type to directory',
    'l=b' => 'specify file type to symlink',
    'f=b' => 'specify file type to normal file',
    :radio
);
for <d l f> -> $t {
    $optset.set-callback(
        $t,
        -> $, $ { @files = @files.grep({ ."{$t}"(); }); }
    );
}
$optset.push(
    'size=i',
    'the minimum size limit of file.',
    callback => sub ($, $size) {
        @files = @files.grep({ .s() >= $size.Int; });
    }
);
$optset.insert-main(
    sub main($optset, @args) {
        return 0 if $optset<help> ^ $optset<version>;
        if $optset.get-pos('directory', 0).?success {
            @args.shift;
        } else {
            &ga-want-helper();
        }
        my $regex = +@args > 0 ?? @args.shift.value !! "";

        if $regex eq "" {
            .path.say for @files;
        } else {
            .path.say if .path ~~ /<$regex>/ for @files;
        }
    }
);

&getopt($optset, :autohv, parser => &ga-parser2);

sub find($dir) {
    for $dir.dir() -> $f {
        take $f;
        if $f ~~ :d {
            &find($f);
        }
    }
}
-------------------------------


.The help of script:
[source,sh]
-------------------
Usage:
./sample/find-file.p6 [directory] OPTIONs

-d               specify file type to directory

-v|--version     print program version.

--size=<integer> the minimum size limit of file.

-?               same as -h.

-h|--help        print this help.

-f               specify file type to normal file

-l               specify file type to symlink
-------------------

= DESCRIPTION

`Getopt::Advance` is a powerful command line option parsing tool, it support many style such as: **unix-style**,
**gnu-style** etc.
It can support `Option` have type such as `Integer`, `Array`, `Hash` compared to traditional command line
tools.
And for non-option argument, it provide `NonOption` feature.
In addition, it also provide `Multi` and `Radio` which can manager multiple options relationship.

= USAGE

== OptionSet

`OptionSet` is a set of `Option` as the literal mean. Provide interface manage your
`Options`.

=== new

There are no argument of new:

`my OptionSet $optset .= new();`

=== Option

`Option` is represent an option in the command line argument, for example '-c 42'.

* `push(::OptionSet::D: Option:D $option -\-> OptionSet)`
* `push(::OptionSet::D: Str:D $opt, :$value, :&callback -\-> OptionSet)`
* `push(::OptionSet::D: Str:D $opt, Str:D $annotation, :$value, :&callback -\-> OptionSet)`

.example
[source,perl6]
$optset.push("c|count=i", "set something int", value => 3, callback => sub ($option, $value) { say "get value $value"; });

Above code add an option to the `OptionSet`, which with short name **c** and long name **count**.
The second parameter set annotation of option to __set something int__, it will be used to generate
help message.
The `value` and `callback` are named argument, it will set the default value of option to **3**.
And the `callback` will be called when option value set.

NOTE: The first parameter of `callback` is current **Option** processed, and second is **value** set by user.

* `append(::OptionSet::D: @options -\-> OptionSet)`
* `append(::OptionSet::D: Str:D $opts -\-> OptionSet)`
* `append(::OptionSet::D: *@optpairs where all(@optpairs) ~~ Pair -\-> OptionSet)`

.example
[source,perl6]
$optset.append("c|count=i" => "set something int", "s|string=s" => "set something string")

This code append two `Options` with their name and annotation to the `OptionSet`.

* `get(::OptionSet::D: Str:D $name, Str:D $type = WhateverType)`

.example
[source,perl6]
$optset.get('c', 'i');

This will try to get a `Option` by its name and type, the sub will return **Any** if the `Option` not exists.

* `has(::OptionSet::D: Str:D $name, Str:D $type = WhateverType -\-> Bool)`

.example
[source,perl6]
$optset.has('c', 'i');

This code check if there is a `Option` called **c** and it's type is **i**.

* `Supply(::OptionSet::D: Str:D $name, Str:D $type = WhateverType -\-> Supply)`

.example
[source,perl6]
---------------
$optset.Supply('c').tap(-> \v {
    my ($optset, $option, $v) = @(v);
    # ... more
})
---------------

This will return a Supply. When the `Option` set by user,
current `OptionSet`, current `Option` and the `value` will be emitted.

=== Group Operation

`Group` can manage more than one `Option`, it provide some feature about option.
For example, with radio group, it only accept one option in same time.

* `append(::OptionSet::D: Str:D $opts, :$optional = True, :$multi -\-> ::OptionSet)`
* `append(::OptionSet::D: Str:D $opts, :$optional = True, :$radio -\-> ::OptionSet)`
* `append(::OptionSet::D: :$optional = True, :$multi, *@optpairs where all(@optpairs) ~~ Pair -\-> ::OptionSet)`
* `append(::OptionSet::D: :$optional = True, :$radio, *@optpairs where all(@optpairs) ~~ Pair -\-> ::OptionSet)`

.example
[source,perl6]
$optset.append("c|count=c;s|string=s", :radio);

This add a radio group, it's only allow set one of them in same time.

.example
[source,perl6]
$optset.append("c|count=c;s|string=s", :multi);

This add a multiple group.

=== NonOption Operation

`NonOption` is represent non-option arguments in the command line arguments.

* `insert-main(::OptionSet::D: &callback = sub () { True } -\-> Int)`
* `insert-main(::OptionSet::D: Str:D $name, &callback = sub () { True } -\-> Int)`

.example
[source,perl6]
$optset.insert-main(sub (@args) { .say for @args; });

This insert a `NonOption::Main` to `OptionSet`, it will call with all non-option arguments.

* `insert-cmd(::OptionSet::D: Str:D $name, &callback = sub () { True } -\-> Int)`
* `insert-cmd(::OptionSet::D: Str:D $name, Str:D $annotation, &callback = sub () { True } -\-> Int)`

.example
[source,perl6]
$optset.insert-cmd("load", sub (@args) { .say for @args; True; } );

This insert a `NonOption::Cmd` to `OptionSet`, it will call with all non-option arguments except the name(load).
It need user provide the command in the command line arguments, for example `scrpt.p6 load *OPTIONs`.

* `insert-pos(::OptionSet::D: Str:D $name, &callback = sub () { True }, :$front, :$last -\-> Int)`
* `insert-pos(::OptionSet::D: Str:D $name, Str:D $annotation, &callback = sub () { True }, :$front, :$last -\-> Int)`
* `insert-pos(::OptionSet::D: Str:D $name, $index where Int:D | WhateverCode , &callback = sub () { True } --> Int)`
* `insert-pos(::OptionSet::D: Str:D $name, Str:D $annotation, $index where Int:D | WhateverCode , &callback = sub () { True } --> Int)`

.example
[source,perl6]
$optset.insert-pos("load", 0, sub ($arg) { say $arg; True; });

This insert a `NonOption::Pos` to `OptionSet`, it will call with non-option argument matched by index(base on zero).
The `front` and `last` will insert a `NonOption::Pos` with index 0 and * - 1, respectively.

* `get(::OptionSet::D: Int:D $id)`
* `get-main(::OptionSet::D:)`
* `get-main(::OptionSet::D: Int:D $id)`
* `get-main(::OptionSet::D: Str:D $name)`
* `get-cmd(::OptionSet::D:)`
* `get-cmd(::OptionSet::D: Int:D $id)`
* `get-cmd(::OptionSet::D: Str:D $name)`
* `get-pos(::OptionSet::D:)`
* `get-pos(::OptionSet::D: Int:D $id)`
* `get-pos(::OptionSet::D: Str:D $name, $index)`

.example
[source,perl6]
$optset.get(2);
$optset.get-main(2);

The first line get an `NonOption` which id is 2.
The second line get the `NonOption::Main` which id is 2.

* `has(::OptionSet::D: Int:D $id -\-> False)`

.example
[source,perl6]
$optset.has(2);

Return True if the `NonOption` which id is 2 exists.

* `Supply(::OptionSet::D: Int:D $id -\-> Supply)`
* `Supply(::OptionSet::D: Int:D $id, :$main!, -\-> Supply)`
* `Supply(::OptionSet::D: Int:D $id, :$cmd! , -\-> Supply)`
* `Supply(::OptionSet::D: Int:D $id, :$pos! , -\-> Supply)`

.example
[source,perl6]
---------------------
$optset.Supply(2).tap(sub \v {
    my ($optset, $no, $v) = @(v);
    #... do more
});
---------------------

This will return a Supply. When the `NonOption` set by user,
current `OptionSet`, current `NonOption` and the `value` will be emitted.

== Option

`Option` can be create by a string.
For example, `"a|action=b"` represent an option with two kind name,
long name is `action`, and short name is `a`, and the option type is `boolean`.
So you can set **action** argument to true by append `-a` or `--action`
after your program.

The list of Option type:

.OptionType
[cols="h,^.^,^.^,^.^m,^.^2m,^.^3m",options="header",width="100%",separator=^]
|==================================
^type ^has argument ^represent ^create example ^set example ^support style
^boolean ^no  ^b ^a|action=b ^-a   ^unix gnu x bsd deactivate
^integer ^yes ^i ^a|action=i ^-a=1 ^unix gnu x
^float   ^yes ^f ^a|action=f ^-a 0.1^unix gnu x
^string  ^yes ^s ^a|action=s ^--action "u" ^unix gnu x
^array   ^yes ^a ^a|action=a ^--action "item" ^unix gnu x
^hash    ^yes ^h ^a|action=h ^-a ":answer(42)" ^unix gnu x
|==================================

== NonOption

`NonOption` can be use to handle `NOA`(short for non-option argument). You can get
specific `NOA`, match them or call `callback` when matched.
And value of the `NonOption` will set to return value of callback, default is True.
The `NOA` index begin from **0**.

NOTE: The `ga-parser` will process `NonOption` after all option argument matched after the value set.
And `ga-parser2` will process `CMD` and `POS` before the `Option` value set, after process over.
Then set the value, process the `MAIN` feature. Check the link:../../sample/find-file.p6[find-file] for more.

=== POS

`NonOption` `POS` capture `NOA` of specific position, but it's not force user supply an
`NOA`. The parser will call it's callback when the `NOA` index matched.

NOTE: The parser will process `POS` after `CMD`, and before `main`.
The `POS` with index is 0 (i.e. front POS) maybe matched when `CMD` not matched.

=== CMD

`NonOption` `CMD` always capture the first NOA, and it's force user supply one of
available `CMDs`. The parser will call it's callback when first `NOA` name matched.

NOTE: The parser will process `CMD` first. If a `POS` (with index 0) provide,
parser will not throw exception when `POS` matched, otherwise it will raise an exception
when all `CMD` not matched.

=== MAIN

`NonOption` `MAIN` capture all `NOAs`, and it's callback will be called by the parser.

NOTE: The parser will process `main` last.

==== Return value

`ga-parser` or `ga-parser2` will generate a return value of each `MAIN` like this:
[source,perl6]
-----------------------------
ReturnValue.new(
    optionset   => $optset,         # current optionset
    noa         => $parserobj.noa,  # current NOA, Array of Argument
    parser      => $parserobj,
    return-value=> do {             # return value of each MAIN
        my %rvs;
        for %($optset.get-main()) {
            %rvs{.key} = .value.value;
        }
        %rvs;
    }
);
-----------------------------
And for `ga-pre-parser`, it set the **noa** to left command line arguments, not the Array of `Argument`.

== Group

`Group` provide a way set up association between multiple `Option`, and it has a check method
which can force the `Group` must be set.
The parser will call it's check method after `Option` value set.

=== radio

In `radio` `Group`, `Option` can be set only one at the same time. And it force user
supply an option when `Group` is not optional.

=== multi

`multi` `Group` is just provide a possibility in having better style of source code.
Also it force user supply an option when `Group` is not optional.

== getopt

Sub `&getopt` accept one or multiple `OptionSet`, pass it and command line argument to the `parser`.
`&getopt` can accept traditional getopt(in C) string, and convert it to `OptionSet`.
Once an `OptionSet` matching success, it will return an instance of `ReturnValue`.
The return value contain matched `OptionSet`、all `NOA(Non-Option Argument)`, and `MAIN`'s id with the return value.

== wrap-command

Using `&wrap-command` wrap your command, provide new command line option or even new feature.
It using `&getopt` parse the command line, then call `&tweak`(provide by user) modify the
left command line argument(member `noa` of return value),
then pass all the command line argument to your command.Here is an example:

.wrapfind
[source, p6]
-------------------------------
use Getopt::Advance;

constant CMD = "find";

wrap-command(
    OptionSet.new.push("e|extension=s"),  # add -e | --extension option to find command line
    CMD,
    tweak => sub ($os, $ret) {
        $ret.noa.append("-iname", "*." ~ $os<e>) if $os<e>;
    }
);
-------------------------------

= REFERENCE

== `&getopt`

Sub `&getopt` accept one or multiple `OptionSet`, pass it and command line argument to the `parser`.
`&getopt` can accept traditional getopt(in C) string, and convert it to `OptionSet`.
Once an `OptionSet` matching success, it will return an instance of `ReturnValue`.
The return value contain matched `OptionSet`、all `NOA(Non-Option Argument)`, and `MAIN`'s id with the return value.

=== Return value

* class ReturnValue
**    has $.optionset;
+
The OptionSet which mathed
**    has @.noa;
+
The left non-option argument depends on the parser
**    has %.return-value;
+
Return value of `MAIN`, the key is `MAIN`'s id.

=== The help message

Sub `&getopt` will display the help message in the following cases:

* all of OptionSet match failed
* `&ga-want-helper` called
* `&ga-want-all-helper` called
* The OptionSet has option named set by `&set-autohv` , and `:autohv` passed to `&getopt`.

=== Exception

If an `OptionSet` match failed, consider follow several situation:

* default
+
Will throw the exception.

* X::GA::ParseError
* X::GA::OptionError
* X::GA::GroupError
* X::GA::NonOptionError
+
The `parser`(except `&ga-pre-parser`) will call `&ga-try-next` throw an
X::GA::ParseError exception when `OptionSet` match failed.
When `&getopt` caught this exception, it will try next `OptionSet` supplied.
If no more `OptionSet`, it will print helper (when help generator `&helper` defined)
of all `OptionSet`,  print error message, and rethrow the exception.
The user also can throw this exception.

* X::GA::WantPrintHelper
+
The user can call `&ga-want-helper` ask `parser` interrupt the parsing process,
and print help message of current `OptionSet`. `&getopt` will print helper (when
help generator `&helper` defined) of current `OptionSet` and exit with 0.

* X::GA::WantPrintAllHelper
+
The user can call `&ga-want-all-helper` ask `&parser` interrupt the parsing process,
and print help message of all `OptionSet`. `&getopt` will print helper(when
help generator `&helper` defined) of all `OptionSet` and exit with 0.

=== Candidates

* getopt(@args = @*ARGS, Str $optstring, *%args)
* getopt(@args = @*ARGS, *@optsets,      *%args)

==== The positional arguments

* @args
+
This is the command line argument passed to `&getopt`, the default value is `@*ARGS`.
And it means you can provide yours "command line arguments" to `&getopt`.

NOTE: The sub `&getopt` will not modify `@*ARGS`.

* $optstring
+
`&getopt` also work fine with traditional Option string (in C) to .

* `*@optsets`
+
The `OptionSet` supplied by user.

==== The named arguments

* :&helper = &ga-helper
+
`&helper` will generate and display help message of `OptionSet`, default is `&ga-helper`.

* :$stdout = $*OUT
+
Current not used, default is `$*OUT`.

* :$stderr = $*ERR
+
Help message will print to `$stderr`, default is `$*ERR`.

* :$parser = &ga-parser
+
Command line argument parser, default is `&ga-parser`, other can be `&ga-pre-parser`, `&ga-parser2`.

* :$parserclass
+
Before parsing, `&getopt` will get a instance of `&parserclass`, then pass it to `&parser` when parsing.

* :$strict = True
+
When `$strict` is True, argument of an option should not be start with option prefix, default is `-` or `--`.

* :$autohv = False
+
If `$autohv` is True, `&getopt` will automate print `$version` information and help message
when the related option set.
The `:autohv` will not work if you don't have a option in `@autohv-opt`,
default is `help` and `version`. Set the option name by sub `set-autohv(@ops)`.
Without `:autohv`, you need deal the help option manually.

NOTE: Will skip the `MAIN` if the `autohv` is triggered.

* :$version
+
Program version information.

* :$bsd-style = False
+
When `$bsd-style` is True, `&parser` will accept bsd style option.

* :@styles = [ :long, :xopt, :short, :ziparg, :comb ]
+
The style need support by the `parser`.

* :@order = []
+
The priority of the styles, default is default order.

* *%args
+
The left named arguments will also pass to `new` of `$parserclass`,
`&helper` and `&parser`.

== `&wrap-command`

* sub wrap-command(OptionSet $os, $cmd, @args is copy = @*ARGS, :&tweak, *%args) is export

** $cmd
+
The command you want wraped.

** @args
+
The command line arguments, default is `@*ARGS`.

** :&tweak
+
Callback provide by user, signature should be `(OptionSet $os, Getopt::Advance::ReturnValue $ret)`.

** *%args
+
Left parameters will pass to `&getopt`.

NOTE: &wrap-command using `run` run your command.

== OUTDATE 

== `OptionSet`

`OptionSet` is a set of `Option` as the literal mean.
It provide a lot of interface can manage many `Option`.

* support operator
+
`OptionSet` support `{}` operator, and the `:exists` adverb.
You can use `{}` access value of option.
And use `:exists` check if the option exists.

=== Method of option

* from-optstring(Str $optstring is copy)
+
This method can convert the traditional option string, and create an OptionSet
contain options descripte by that string.

NOTE: You should use `&getopt(@args = @*ARGS, Str $optstring, *%args)` instead of this method.

* keys(::?CLASS::D:)
+
Return the name of all options.

* values(::?CLASS::D:)
+
Return all options of this `OptionSet`.

* get(::?CLASS::D: Str:D $name --> Option)
+
Return an option has the name `$name`.
If it not exist, it will return an type object `Option`.

* has(::?CLASS::D: Str:D $name --> Bool)
+
Return True if the option exist.

* has(::?CLASS::D: Str:D @name --> Bool)
+
Return True if all of the option exist.

* remove(::?CLASS::D: Str:D $name --> Bool)
+
Remove the option with the name `$name`, or return False if the option not exist.

* remove(::?CLASS::D: Str:D @names --> Bool)
+
Remove all the options, it will return False if any option not exist.

* reset(::?CLASS::D: Str:D $names) of ::?CLASS
+
Reset the option to default value. It will call `reset-value` of the option.

* reset(::?CLASS::D: Str:D @names) of ::?CLASS
+
Reset all the options to default value.

* set-value(::?CLASS::D: Str:D $name, $value, :$callback = True) of ::?CLASS
+
Set the option's value to `$value`, and call the callback of option when `callback`
setted.

* set-annotation(::?CLASS::D: Str:D $name, Str:D $annotation) of ::?CLASS
+
Set the option's annotation, the annotation will be print by `&helper`.

* set-callback(::?CLASS::D: Str:D $name, &callback) of ::?CLASS
+
Set the callback of option. The callback will be called when option's value setted.

* push
** push(::?CLASS::D: Str:D $opt, :$value, :&callback) of ::?CLASS
** push(::?CLASS::D: Str:D $opt, Str:D $annotation, :$value, :&callback) of ::?CLASS

+
Add an option to the `OptionSet`. You can use `:$value` set it's default value.
And the same of callback. The `$annotation` is the help message of the option.

* append
** append(::?CLASS::D: Str:D $opts) of ::?CLASS
** append(::?CLASS::D: Str:D $opts, :$optional = True, :$radio!) of ::?CLASS
** append(::?CLASS::D: Str:D $opts, :$optional = True, :$multi!) of ::?CLASS
** append(::?CLASS::D: *@optpairs where all(@optpairs) ~~ Pair, :$radio where :!so, :$multi where :!so) of ::?CLASS
** append(::?CLASS::D: :$optional = True, :$radio!, *@optpairs where all(@optpairs) ~~ Pair) of ::?CLASS
** append(::?CLASS::D: :$optional = True, :$multi!, *@optpairs where all(@optpairs) ~~ Pair) of ::?CLASS

+
Add multiple options to the `OptionSet`, make sure the string is split by `;`.
You can make them as a radio group or multi group by pass `:radio`、`:multi`.

=== Method of non-option

Like option identified by its name, every non-option has an integer id.
The `cmd` and `pos` has a state identify they triggered successful, and
the method `success` of `cmd` and `pos` will return True.

* has(::?CLASS::D: Int:D $id ) of Bool
+
Return True if the non-option exists.

* remove(Int:D $id)
+
Remove the non-option.

* get-main(::?CLASS::D:) of Hash
+
Return all `main` s of the `OptionSet`.

* get-main(::?CLASS::D: Int $id) of NonOption::All
+
Return the `main` with id `$id`, or return undefined.

* get-cmd(::?CLASS::D:) of Hash
+
Return all `cmd` s of the `OptionSet`.

* get-cmd(::?CLASS::D: Int $id) of NonOption::Cmd
+
Return the `cmd` with id `$id`, or return undefined.

* get-cmd(::?CLASS::D: Str $name) of NonOption::Cmd
+
Return one of `cmd` s with name `$name`, or return undefined.

* get-pos(::?CLASS::D:) of Hash
+
Return all `pos` s of the `OptionSet`.

* get-pos(::?CLASS::D: Int $id) of NonOption::Pos
+
Return the `pos` with id `$id`, or return undefined.

* get-pos(::?CLASS::D: Str $name, $index) of NonOption::Pos
+
Return one of `cmd` s which match the index `$index` and the name `$name`,
or return undefined.

* reset-cmd(::?CLASS::D: Int $id)
+
Reset the state of `cmd` with id `$id` to False.

* reset-cmd(::?CLASS::D: Str $name)
+
Reset the state of all `cmd` s with name `$name` to False.

* reset-pos(::?CLASS::D: Int $id)
+
Reset the state of `pos` with id `$id` to False.

* reset-pos(::?CLASS::D: Str $name, $index)
+
Reset the state of all `pos` s with name `$name` to False.

* insert-main(::?CLASS::D: &callback) of Int
+
Insert a `main` non-option to the `OptionSet`, and return its id.
The `main` 's callback will be called by `&parser` before return to `&getopt`,
with the `OptionSet` and all the non-option arguments.
The `main` is process after `pos` and `cmd`.

* insert-cmd(::?CLASS::D: Str:D $name) of Int
+
Insert a `cmd` with the name `$name`, and return its id.
If the `cmd` set by user, the method `success` of it will return True.
The `cmd` is process before `pos` and `main`.

* insert-cmd(::?CLASS::D: Str:D $name, &callback) of Int
+
Insert a `cmd` with the name `$name`, and return its id.
If the `cmd` set by user, the method `success` of it will return True.
And the callback will be called with the `OptionSet` and all the non-option
arguments except `cmd` name.
The `cmd` is process before `pos` and `main`.

* insert-pos(::?CLASS::D: Str:D $name, &callback, :$front!) of Int
+
Insert a front `pos` with the name `$name`, and return its id.
If the `pos` set by user, the method `success` of it will return True.
And the callback will be called with the `OptionSet` and the front(the first) non-option
argument(the `cmd` name will be exclude from the non-option arguments).
The front `pos` is process before the other `pos`.

* insert-pos(::?CLASS::D: Str:D $name, &callback, :$last!) of Int
+
Insert a last `pos` with the name `$name`, and return its id.
If the `pos` set by user, the method `success` of it will return True.
And the callback will be called with the `OptionSet` and the last non-option
argument(the `cmd` name will be exclude from the non-option arguments).

* insert-pos(::?CLASS::D: Str:D $name, $index where Int:D | WhateverCode , &callback) of Int
+
Insert a `pos` with the name `$name`, and return its id.
If the `pos` set by user, the method `success` of it will return True.
And the callback will be called with the `OptionSet` and the non-option
argument matched the index(the `cmd` name will be exclude from the non-option arguments).

* check(::?CLASS::D:)
+
Check `radio`, `multi` group and non-optional option.

* annotation(::?CLASS::D:)
+
Use `Terminal::Table` format help message, and return it as an array.

* merge(::?CLASS::D: ::?CLASS:D $os) of ::?CLASS
+
Merge the `Option`s and `NonOption`s of `$os` to current `OptionSet`.

* clone(*%_)
+
Return an deep copy of current `OptionSet`.

== `Argument`

This is the command line argument, include the index and value.
The `&ga-parser` and `&ga-pre-parser` use non-option argument create `Argument`, and pass it to `cmd`,
`pos` or `main`.

=== Method

* index() of Int
+
Return index (base on zero) of the command line argument.

* value() of Str
+
Return value of the command line argument.

* pairup() of Pair
+
Construct an Pair use the index and value.

* clone(*%_)
+
Return an deep copy of current `Argument`.

== `Exception`

* X::GA::ParseFailed
+
Exception thrown when `&parser` parse failed.

NOTE: thrown by `ga-try-next(Str $msg)`.

* X::GA::OptionInvalid
+
Exception thrown when `Option's` value is invalid.

NOTE: thrown by `ga-invalid-value(Str $msg)`.

* X::GA::PosCallFailed
+
Exception thrown by user.

NOTE: thrown by `ga-try-next-pos(Str $msg)`.

* X::GA::Error
+
Exception thrown when Option string or Non-Option index invalid.

NOTE: thrown by `ga-raise-error(Str $msg)`.

* X::GA::WantPrintHelper
+
Exception thrown when user want print help message of current
`OptionSet`.

NOTE: thrown by `ga-want-helper()`.

* X::GA::WantPrintAllHelper
+
Exception thrown when user want print help message of all
`OptionSet`.

NOTE: thrown by `ga-want-all-helper()`.

* X::GA::GroupValueInvalid
+
Exception thrown when `Group` check failed.

NOTE: thrown by `ga-group-error(Str $msg)`.

== `Option`

The `Option` represent an option of command line argument.
It can have long or short name, annotation, and
callback which will called when option value set by user.

=== Method of role Option

* value()
+
Return the value of the option.

* long() of Str
+
Return the long name of the option, return empty string when not setted.

* short() of Str
+
Return the short name of the option, return empty string when not setted.

* callback()
+
Return the callback of the option.

* optional() of Bool
+
Return True if the option is optional. Default is True.

* annotation() of Str
+
Return the annotation of the option, return empty string when not setted.

* default-value()
+
Return the default value of the option.
The default value is value set by programmer when program initialized.

* has-value() of Bool
+
Return True if value defined.

* has-long() of Bool
+
Return True if the option has long name.

* has-short() of Bool
+
Return True if the option has short name.

* has-callback() of Bool
+
Return True if the option has callback.

* has-annotation() of Bool
+
Return True if the option has annotation.

* has-default-value() of Bool
+
Return True if the option has default value.

* set-value(Mu, Bool :$callback)
+
Set the value of the option, it will call `&callback` when `:callback` passed.
The child class will check the type of value.

* set-long(Str:D)
+
Set the long name of the option. You should not call this method directly.

* set-short(Str:D)
+
Set the short name of the option. You should not call this method directly.

* set-callback(&callback)
+
Set the callback of the option. You should not call this method directly.

* set-optional(Mu)
+
Set the option optional. You should not call this method directly.

* set-annotation(Str:D)
+
Set the annotation of the option. You should not call this method directly.

* set-default-value(Mu)
+
Set the default value of the option. You should not call this method directly.

* reset-long()
+
Set the long name to empty string.

* reset-short()
+
Set the short name to empty string.

* reset-value()
+
Set the value to default value.

* reset-callback()
+
Set the callback to `(Callable)`.

* reset-annotation()
+
Set the annotation to empty string.

* type() of Str
+
Return the identify name of the option class.

* check() of Bool
+
Return False when value not set and the option not optional.

* match-name(Str:D) of Bool
+
Return True if the name equal to any name of the option.

* match-value(Mu) of Bool
+
Return True if the value type check success.

* lprefix()
+
Return the long prefix of the option, this method used by `&usage`.

* sprefix()
+
Return the short prefix of the option, this method used by `&usage`.

* need-argument()
+
Return True if the option need argument.

* usage() of Str
+
Return the usage of the option.

* clone(*%_)
+
Return an deep copy of current option.

== `NonOption`

The `NonOption` represent NOA(non-option argument) of command line argument.
It can support create multi command program, or something like `MAIN` of Perl6.

=== Method of role NonOption

* success() of Bool
+
Return True when the non-option callback called successful.

* callback()
+
Return callback of the non-option.

* set-callback(&callback)
+
Set callback of the non-option.
The child class of `NonOption` will check the callback
signature.

* has-callback() of Bool
+
Return True when the non-option has callback.

* match-index(Int $total, Int $index)
+
Check index of the non-option, `$total` is the
number of command line non-option arguments.

* match-name(Str $name)
+
Return True if `$name` equal non-option's name.

* reset-success()
+
Reset the status of non-option.

* type() of Str
+
Return the identify name of the non-option class.

* usage() of Str
+
Return the usage of the non-option.

* clone(*%_)
+
Return an deep copy of current non-option.

=== Method of NonOption::Pos

* value()
+
It will be set when current `pos` matched.

== `Group`

=== Method of role Group

* usage() of Str
+
Return the usage message of current group.

* has(Str:D $name) of Bool
+
Return True if current group has option `$name`.

* remove(Str:D $name where $name !~~ /^\s+$/) of Bool
+
Return True if success remove option `$name`.

* check()
+
Check the value of options in current group.

* clone(*%_)
+
Return an deep copy of current group.

== `Parser`

The `&ga-parser` and `&ga-pre-parser` will first check the options,
it will set the value provide by user when all option matched.
Then check the options inside group.
And next check `cmd` and `pos`, call its callback when matched.
In the end, it will call all `main`s and return all the return value.

* sub ga-parser( @args, $optset,
    :$strict,
    :$x-style where :!so,
    :$bsd-style,
    :$autohv ) of Getopt::Advance::ReturnValue

* sub ga-parser( @args, $optset,
    :$strict,
    :$x-style where :so,
    :$bsd-style,
    :$autohv ) of Getopt::Advance::ReturnValue

* sub ga-pre-parser(
    @args,
    $optset,
    :$strict,
    :$x-style where :!so,
    :$bsd-style,
    :$autohv
) of Getopt::Advance::ReturnValue

* sub ga-pre-parser(
    @args,
    $optset,
    :$strict,
    :$x-style where :so,
    :$bsd-style,
    :$autohv
) of Getopt::Advance::ReturnValue
+
`ga-parser` will return the return value of each main except autohv
passed and user set `help` or `version` option.

NOTE: `ga-pre-parser` will not cause any error if an `Option` not exists,
or `NonOption` not matched. And it will set `noa` of return value to all the aguments of command line ,
not an Array of `Argument`.


== `Types`

=== Method of class Types::Manager

* has(Str $name) of Bool
+
Return True if manager has type `$name`.

* innername(Str:D $name) of Str
+
Return the name of type .

* register(Str:D $name, Mu:U $type) of ::?CLASS:D
+
Registe a type to the manager.

* create(Str $str, :$value, :&callback)
* create(Str $str,  Str:D $annotation, :$value, :&callback)
+
Create a option of type with the given option string.

* clone(*%_)
+
Return an deep copy of current manager.
